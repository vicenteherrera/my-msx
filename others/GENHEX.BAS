2000 'OOOOOOOOOOOOOOOOOOOOOOOO
2010 'OOOOO   PROGRAMA   OOOOO
2020 'OOOO  GENERADOR DE  OOOO
2030 'OOOOO   SPRITES    OOOOO
2040 'OOO                  OOO
2050 'OO LUIS SANGUINO 1986 OO
2060 'OOOOOOOOOOOOOOOOOOOOOOOO
2310 COLOR 1,14,14
2320 SCREEN 1:KEY OFF
2330 CLEAR 3000
2340 OPEN "GRP:"AS#1
2350 DIM DA(12),O(256)
2360 FOR I=0 TO 11:READ DA(I):NEXT
2490 LOCATE 0,22: PRINT"PULSA [ENTER] PARA CONTINUAR"
2500 IF INKEY$=CHR$(13) THEN CLS :BEEP ELSE 2500
2510 LOCATE4,1:PRINT"OOOOO OPCIONES OOOOO"
2520 LOCATE 1,4: PRINT"PULSA UNO DE ESTOS NUMEROS"
2530 LOCATE 0,9:PRINT"[0]=SPRITES 8*8 SIN AMPLIAR
2540 LOCATE 0,11:PRINT"[1]=SPRITES 8*8 AMPLIADOS"
2550 LOCATE 0,15:PRINT"[2]=SPRITES 16*16 SIN AMPLIAR"
2560 LOCATE 0,17:PRINT"[3]=SPRITES 16*16 AMPLIADOS"
2570 X$=INKEY$
2580 IF X$="0"THEN T=0:RI=1:GOTO 2630
2590 IF X$="1"THEN T=1:RI=1:GOTO 2630
2600 IF X$="2"THEN T=2:RI=8:GOTO 2630
2610 IF X$="3"THEN T=3:RI=8:GOTO 2630
2620 GOTO 2570
2630 CLS:BEEP
2640 LOCATE 0,12:PRINT"OO JOYSTICK O CURSOR [J/C] OO"
2650 X$=INKEY$
2660 IF X$="J"OR X$="j" THEN CT=1:GOTO 2690
2670 IF X$="c"OR X$="C" THEN CT=0:GOTO 2690
2680 GOTO 2650
2690 BEEP:SCREEN 2,T
2700 N=N+1
2710 CLS:RO=RO+1
2720 O(RO)=N
2730 CLS
2740 NP=NP+1
2750 LINE(0,0)-(255,10),2,BF
2760 IF T=0 THEN PSET(5,2),14:PRINT#1,"OOO SPRITES 8*8 SIN AMPLIAR OOO":GOTO 2800
2770 IF T=1 THEN PSET(5,2),14:PRINT#1,"OOOO SPRITES 8*8 AMPLIADOS OOOO":GOTO 2800
2780 IF T=2 THEN PSET(5,2),14:PRINT#1,"OOOSPRITES 16*16 SIN AMPLIAROOO":GOTO 2800
2790 IF T=3 THEN PSET(5,2),14:PRINT#1,"OOO SPRITES 16*16 AMPLIADOS OOO":GOTO 2800
2800 PSET(5,15),14:PRINT#1," OOOO NUMERO DE ORDEN =";N;"OOOO"
2810 PSET(5,30),14:PRINT#1,"OOOO NUMERO DE PORTADOR=";NP;"OOOO"
2820 LINE (180,48)-(250,70),7,BF
2830 PSET (190,50),14:PRINT#1,"MUESTRA"
2840 PSET(210,60),7:PRINT#1,"NO"
2850 IF REP=1 THEN FOR I=1 TO 3:LINE (0,48)-(255,72),13,BF:PSET(4,52),13:PRINT#1,"REPETIMOS MISMA FIGURA CON OTRO":PSET(100,62),13:PRINT#1,"PORTADOR":FOR J=1 TO 250:NEXTJ:NEXT:GOTO 3640
2860 PSET(4,180),14:PRINT#1,"PULSA [SELECT] PARA OBTENER DATA"
2870 IF T=1 OR T=3 THEN TA=3:Z=0 ELSE TA=11:Z=4
2880 B$="": FOR S=Z TO TA
2890 B$=B$+CHR$(DA(Z)):NEXT
2900 SPRITE$(0)=B$
2910 IF T=0 OR T=1 THEN A1=95:B1=64:A2=159:B2=128:ST=8:PY=140:PX=120
2920 IF T=2 OR T=3 THEN A1=47:B1=40:A2=175:B2=168:ST=32:PY=95:PX=207
2930 LINE(A1,B1)-(A1,B2),1
2940 LINE(A1,B2)-(A2,B2),1
2950 LINE(A2,B2)-(A2,B1),1
2960 IF ST=32 THEN LINE(111,40)-(111,168),1:LINE(47,104)-(175,104),1
2970 LINE(A2,B1)-(A1,B1),1
2980 Y=B1+8:H=103:V=72:SAL=0:MU=1:SEL=0
2990 FOR X=A1 TO A2 STEP 2
3000 PSET(X,Y),1
3010 NEXT
3020 Y=Y+8:IF Y=B2 THEN 3030 ELSE 2990
3030 X=A1+8
3040 FOR Y=B1 TO B2 STEP 2
3050 PSET(X,Y),1
3060 NEXT
3070 X=X+8:IF X=A2 THEN 3080 ELSE 3040
3080 P=STICK(CT)
3090 IF P=1 THEN V=V-8
3100 IF P=2 THEN V=V-8:H=H+8
3110 IF P=3 THEN H=H+8
3120 IF P=4 THEN H=H+8:V=V+8
3130 IF P=5 THEN V=V+8
3140 IF P=6 THEN V=V+8:H=H-8
3150 IF P=7 THEN H=H-8
3160 IF P=8 THEN H=H-8:V=V-8
3170 IF V<B1 THEN V=B1
3180 IF V>B2-8 THEN V=B2-8
3190 IF H<A1 THEN H=A1
3200 IF H>A2-8 THEN H=A2-8
3210 FOR I=1 TO 25:NEXT
3220 PUT SPRITE0,(H,V),1,0
3230 X$=INKEY$
3240 IF STRIG(CT)=-1THEN GOSUB 3300
3250 IF X$=CHR$(27)THEN LINE(H+1,V+1)-(H+7,V+7),14,BF:GOSUB 3310
3260 IF X$=CHR$(24)THEN SEL=1 :GOTO 3320
3270 IF X$="M" OR X$="m" THEN MU=-MU:BEEP:LINE(209,59)-(229,70),7,BF:GOSUB 3290
3280 GOTO 3080
3290 IF MU=1 THEN PSET(210,60),7:PRINT#1,"NO":RETURN ELSE PSET(210,60),7:PRINT#1,"SI":GOTO 3320
3300 LINE(H+1,V+1)-(H+7,V+7),4,BF
3310 IF MU=1 THEN RETURN
3320 FOR J=B1+1 TO B2-7 STEP 8
3330 FOR I=A1+2 TO A1+58 STEP 8
3340 IF POINT(I,J)=4 THEN PUT SPRITE 0,(H,209),1,0:GOTO 3350 ELSE 3430
3350 IF I=A1+2 THEN D=D+128
3360 IF I=A1+10 THEN D=D+64
3370 IF I=A1+18 THEN D=D+32
3380 IF I=A1+26 THEN D=D+16
3390 IF I=A1+34 THEN D=D+8
3400 IF I=A1+42 THEN D=D+4
3410 IF I=A1+50 THEN D=D+2
3420 IF I=A1+58 THEN D=D+1
3430 NEXT
3440 L=(J-(B1+1))/8
3450 VPOKE 14336+L+(N*ST),D
3460 D=0:NEXT
3470 IF ST=8 THEN 3640
3480 D=0
3490 FOR YO =0 TO 15
3500 YA=(B1+1)+YO*8
3510 FOR I=A1+66 TO A2-6 STEP 8
3520 IF POINT(I,YA)=4 THEN PUT SPRITE0,(H,209),1,0:GOTO 3530 ELSE 3610
3530 IF I=A1+66 THEN D=D+128
3540 IF I=A1+74 THEN D=D+64
3550 IF I=A1+82 THEN D=D+32
3560 IF I=A1+90 THEN D=D+16
3570 IF I=A1+98 THEN D=D+8
3580 IF I=A1+106 THEN D=D+4
3590 IF I=A1+114 THEN D=D+2
3600 IF I=A1+122 THEN D=D+1
3610 NEXT
3620 VPOKE 14352+YO+(N*ST),D
3630 D=0:NEXT
3640 VPOKE 6912+(NP*4),PY
3650 VPOKE 6913+(NP*4),PX
3660 VPOKE 6914+(NP*4),N*ST/8
3670 VPOKE 6915+(NP*4),1
3680 IF REP=1 THEN 3890
3690 IF SEL=1 THEN 3710
3700 BEEP:RETURN
3710 LD=0:VI=180
3720 IF ST=32 THEN VI=162
3730 LINE(A1,B1)-(A2,B2),14,BF:PUT SPRITE 0,(H,209),1,0
3740 LINE(0,VI-2)-(255,192),15,BF
3750 PSET(0,VI),15:PRINT#1,"DATA";
3760 FOR I=1 TO ST
3770 TI=(30+(IM*12)+REC)
3780 IM=IM+1
3790 DAT=VPEEK(14335+I+N*ST)
3800 DAT$=HEX$(DAT)
3810 PSET(TI+5,VI),15:PRINT #1,DAT$
3820 IF I=1 THEN 3850
3830 PSET(TI,VI),15:PRINT #1,","
3840 IF DAT<16 THEN LD=0
3850 IF DAT>15 THEN LD=1
3860 REC=REC+(LD*8)
3870 IF TI+(LD*8)>=224 THEN VI=VI+10:IM=0:REC=0
3880 NEXT
3890 LINE(0,40)-(255,80),11,BF
3900 NM=N :NR=NP:IF NP>12 THEN NR=12:NM=11
3910 IF N*ST>=160 THEN NM=160/ST:NR=NM
3920 IF N*ST>160 OR NP>12 THEN LINE(3,88)-(200,130),6,BF: PSET(32,90),6:PRINT#1,"!!! ATENCION !!!             COPIA LAS DATAS A MANO          A PARTIR DE ESTE SPRITE         ANTES DE CONTINUAR DE LO        CONTRARIO LAS PERDERAS"
3930 PSET(18,45),11:PRINT#1,"PULSA [S] PARA OTRO SPRITE"
3940 PSET(18,58),11:PRINT#1,"PULSA [R] PARA OTRO PORTADOR"
3950 PSET(18,71),11:PRINT#1,"PULSA [T] PARA TERMINAR"
3960 X$=INKEY$
3970 IF X$="S" OR X$="s" THEN LINE(0,0)-(255,192),14,BF:PUT SPRITE NP,(PX,209):REC=0:IM=0:GOTO 2700
3980 IF X$="r" OR X$="R" THEN REP=1:PUT SPRITE NP,(PX,209):GOTO 2710
3990 IF X$="T" OR X$="t" THEN 4020
4000 GOTO 3960
4010 DATA 240,240,240,240,255,255,255,255,255,255,255,255
4020 COLOR 1,15:SCREEN 0
4030  DIM F(200)
4040 CLS
4050 R=1
4060 PRINT "10 SCREEN 2,";T;":FOR K=1 TO";NM;":B$=";CHR$(34);CHR$(34);":FORI=1TO"ST;":READ A$:B$=B$+CHR$(val(";CHR$(34)"&H";CHR$(34);"+A$)):NEXT:SPRITE$ (K)=B$:NEXT"
4070 X$="10":Y$="0":XS=10
4080 FOR J=1 TO NR
4090 COL=COL+1
4100 IF O(J)>R THEN R=R+1
4110 COL$=STR$(COL):R$=STR$(R):J$=STR$(J)
4120 PRINT (J*10)+10;"PUTSPRITE";J$;",(";X$;",";Y$;"),";COL$;",";R$
4130 YS=YS+10+ST:IF YS>185-ST THEN YS=0:XS=XS+13+ST
4140 X$=STR$(XS)
4150 Y$=STR$(YS)
4160 NEXT
4170 GI=(J*10+10)
4180 PRINT GI;"DATA";
4190 FOR I=1 TO  NM*ST
4200 F(I)=VPEEK(14335+I+ST)
4210 HOS$=HEX$(F(I))
4220 PRINT HOS$;
4230 IF I=82 THEN GI=GI+10:PRINT:PRINT GI;"DATA";:GOTO 4260
4240 IF I>=ST*NM THEN 4270
4250 PRINT CHR$(44);
4260 NEXT
4270 PRINT":GOTO ";GI
